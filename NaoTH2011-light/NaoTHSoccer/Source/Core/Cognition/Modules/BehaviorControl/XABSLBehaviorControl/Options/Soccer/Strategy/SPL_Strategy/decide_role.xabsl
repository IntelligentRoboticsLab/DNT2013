/** Behavior that is common to all players (goalie, striker, defender, ...) */
option decide_role
{
  common decision
  {
    if(state_time > 4000 && game.player_number == 1 && team.members_alive_count >= 0)
      goto goalie;
    else if(state_time > 4000 && 
	          (team.members_alive_count <= 1) ||
	          (team.calc_if_is_striker && fall_down_state == upright))
	  {
      if(state_time < 20000 && game.own_kickoff && !ball_was_outside_circle)
	      goto initial_striker;
	    else
	      goto striker;
	  }
	  else if(situation.opponent_at_the_ball && state_time > 4000 && team.relative_position_to_ball == 2)
      goto supporter_aggressive;
    else if((!situation.opponent_at_the_ball && state_time > 4000 && team.relative_position_to_ball == 3) || 
            (situation.opponent_at_the_ball && state_time > 4000 && team.relative_position_to_goal == 4))
      goto supporter;
    else if((!situation.opponent_at_the_ball && state_time > 4000 && team.relative_position_to_goal == 3) ||
            (situation.opponent_at_the_ball && state_time > 4000 && team.relative_position_to_ball == 4))
      goto defender;
    else if(situation.opponent_at_the_ball && state_time > 4000 && team.relative_position_to_goal == 2)
      goto supporter_defensive;
  } 

  state goalie
  {
    action
    {
      set_first_half_right_eye = blue;
      situationStatusOwnHalf = true;
      team.is_playing_as_striker = false;
      ball_was_outside_circle = true;
      spl_robust_goalie();
    }
  }

  initial state striker
  {
    action
    {
      set_first_half_right_eye = red;
      team.is_playing_as_striker = true;
      ball_was_outside_circle = true;
      spl_playing_striker();
      set_left_ear = platform.frameNumber % 10;
    }
  }

  state defender
  {
    action
    {
      set_first_half_right_eye = green;
      team.is_playing_as_striker = false;
      ball_was_outside_circle = true;
      spl_playing_defender();
    }
  }

  state supporter
  {
    action
    {
      set_first_half_right_eye = blue;
      team.is_playing_as_striker = false;
      ball_was_outside_circle = true;
      spl_playing_supporter();
    }
  }

  state supporter_aggressive
  {
    action
    {
      set_first_half_right_eye = blue;
      team.is_playing_as_striker = false;
      ball_was_outside_circle = true;
      spl_playing_aggressive_supporter();
    }
  }

  state supporter_defensive
  {
   action
    {
      set_first_half_right_eye = blue;
      team.is_playing_as_striker = false;
      ball_was_outside_circle = true;
      spl_playing_defensive_supporter();
    }
  }

  state initial_striker
  {
    action
    {
      initial_kick_off();
    }
  }
}
