/** Behavior that is common to all players (goalie, striker, defender, ...) */
option decide_role
{

  common decision 
  {
    /** Goalie */
    if(game.player_number == 1 && team.members_alive_count >= 0) //become field player, when all others are "dead"
    {
      goto goalie;
    }
    /* fallback if everything else is broken */
    else if( (team.members_alive_count <= 1) ||
             (team.get_position == team.position.striker && 
              fall_down_state == upright))
    {
      if(state_time < 20000 &&
         game.own_kickoff && !ball_was_outside_circle)
        goto initial_striker;
      else
        goto striker;
    }
    else if(team.get_position == team.position.defender)
    {
      goto defender;
    }
    else if(team.get_position == team.position.supporter)
    {
      goto supporter;
    }
    else if (team.get_position == team.position.supporter_aggressive)
    {
      goto supporter_aggressive;
    }
  }

  state goalie
  {
    action
    {
      situationStatusOwnHalf = true;
      team.is_playing_as_striker = false;
      ball_was_outside_circle = true;
      spl_robust_goalie();
    }
  }

  initial state striker
  {
    action
    {
      team.is_playing_as_striker = true;
      ball_was_outside_circle = true;
      spl_playing_striker();
      set_left_ear = platform.frameNumber % 10;
    }
  }

  state defender
  {
    action
    {
      team.is_playing_as_striker = false;
      ball_was_outside_circle = true;
      spl_playing_defender();
    }
  }

  state supporter
  {
    action
    {
      team.is_playing_as_striker = false;
      ball_was_outside_circle = true;
      spl_playing_supporter();
    }
  }

  state supporter_aggressive
  {
    action
    {
      team.is_playing_as_striker = false;
      ball_was_outside_circle = true;
      spl_playing_supporter_aggressive();
    }
  }


  state initial_striker
  {
    action
    {
      initial_kick_off();
    }
  }
  
}
